(* Discretization logic for continuous dice *)

open Ast
open Lats

(* Calculate probability for a given concrete distribution in an
   interval.  Used only when all cuts are concrete constants. *)
let prob_cdistr_interval (left : float) (right : float) (dist : Distributions.cdistr) : float =
  let cdf = Distributions.cdistr_cdf dist in
  cdf right -. cdf left

(* Helpers for emitting symbolic float-arithmetic expressions. *)
let mk_const f = ExprNode (Const f)
let mk_sub a b = ExprNode (Sub (a, b))
let mk_cdf d e = ExprNode (Cdf (d, e))

(* Strip type annotations from a [texpr] to recover the underlying
   surface [expr].  Used when emitting CDF terms: the CDF's
   distribution argument must keep its original (possibly symbolic)
   parameters, not their discretized form. *)
let rec texpr_to_expr ((_, _, TAExprNode ae) : texpr) : expr =
  match ae with
  | Var x -> ExprNode (Var x)
  | Const f -> ExprNode (Const f)
  | BoolConst b -> ExprNode (BoolConst b)
  | Let (x, e1, e2) -> ExprNode (Let (x, texpr_to_expr e1, texpr_to_expr e2))
  | Sample d -> ExprNode (Sample (sample_to_expr d))
  | DiscreteCase cases ->
      ExprNode (DiscreteCase (List.map (fun (b, p) -> (texpr_to_expr b, texpr_to_expr p)) cases))
  | Cmp (op, e1, e2, f) -> ExprNode (Cmp (op, texpr_to_expr e1, texpr_to_expr e2, f))
  | FinCmp (op, e1, e2, n, f) -> ExprNode (FinCmp (op, texpr_to_expr e1, texpr_to_expr e2, n, f))
  | FinEq (e1, e2, n) -> ExprNode (FinEq (texpr_to_expr e1, texpr_to_expr e2, n))
  | And (e1, e2) -> ExprNode (And (texpr_to_expr e1, texpr_to_expr e2))
  | Or (e1, e2) -> ExprNode (Or (texpr_to_expr e1, texpr_to_expr e2))
  | Not e1 -> ExprNode (Not (texpr_to_expr e1))
  | If (e1, e2, e3) -> ExprNode (If (texpr_to_expr e1, texpr_to_expr e2, texpr_to_expr e3))
  | Pair (e1, e2) -> ExprNode (Pair (texpr_to_expr e1, texpr_to_expr e2))
  | First e1 -> ExprNode (First (texpr_to_expr e1))
  | Second e1 -> ExprNode (Second (texpr_to_expr e1))
  | Fun (x, e1) -> ExprNode (Fun (x, texpr_to_expr e1))
  | FuncApp (e1, e2) -> ExprNode (FuncApp (texpr_to_expr e1, texpr_to_expr e2))
  | FinConst (k, n) -> ExprNode (FinConst (k, n))
  | Observe e1 -> ExprNode (Observe (texpr_to_expr e1))
  | Fix (f, x, e1) -> ExprNode (Fix (f, x, texpr_to_expr e1))
  | Nil -> ExprNode Nil
  | Cons (e1, e2) -> ExprNode (Cons (texpr_to_expr e1, texpr_to_expr e2))
  | MatchList (e1, en, y, ys, ec) ->
      ExprNode (MatchList (texpr_to_expr e1, texpr_to_expr en, y, ys, texpr_to_expr ec))
  | Ref e1 -> ExprNode (Ref (texpr_to_expr e1))
  | Deref e1 -> ExprNode (Deref (texpr_to_expr e1))
  | Assign (e1, e2) -> ExprNode (Assign (texpr_to_expr e1, texpr_to_expr e2))
  | Seq (e1, e2) -> ExprNode (Seq (texpr_to_expr e1, texpr_to_expr e2))
  | Unit -> ExprNode Unit
  | RuntimeError s -> ExprNode (RuntimeError s)
  | Add (e1, e2) -> ExprNode (Add (texpr_to_expr e1, texpr_to_expr e2))
  | Sub (e1, e2) -> ExprNode (Sub (texpr_to_expr e1, texpr_to_expr e2))
  | Mul (e1, e2) -> ExprNode (Mul (texpr_to_expr e1, texpr_to_expr e2))
  | Div (e1, e2) -> ExprNode (Div (texpr_to_expr e1, texpr_to_expr e2))
  | SpecialFunc (name, args) -> ExprNode (SpecialFunc (name, List.map texpr_to_expr args))
  | Cdf (d, e1) -> ExprNode (Cdf (sample_to_expr d, texpr_to_expr e1))
  | CdfExpr (k, e1) -> ExprNode (CdfExpr (texpr_to_expr k, texpr_to_expr e1))

and sample_to_expr (d : texpr sample) : expr sample =
  match d with
  | Distr1 (k, a) -> Distr1 (k, texpr_to_expr a)
  | Distr2 (k, a, b) -> Distr2 (k, texpr_to_expr a, texpr_to_expr b)

(* Extract the cut-bag from a TFloat type, failing with [msg] on
   shape mismatch. *)
let cut_bag_of (ty : ty) (msg : string) : CutLat.bag =
  match Ast.force ty with
  | TFloat (b, _, _) -> b
  | _ -> failwith ("Type error: " ^ msg)

(*
Discretizer from typed expressions to discrete expressions.

The idea is that the type system infers a bag of cuts that each
float-typed expression possibly compares against.  Instead of
sampling from a continuous distribution, we sample from a discrete
distribution that tells us the probabilities of the interval between
two cut values.

When a comparison is against a cut value, we convert that to a
comparison against the discrete integer that represents the i-th
cut.

Cuts may be concrete (a float constant) or symbolic (an arbitrary
float-valued expression).  When all cuts are concrete the
distribution's probabilities are computed numerically via the GSL
CDF.  When any cut is symbolic, the probabilities are emitted as
symbolic expressions of the form [CDF(d, c2) - CDF(d, c1)].
*)
(* Does a [texpr] (or any sub-expression) contain a [Sample] node?
   Used to detect "sample-flavored" expressions that the
   discretizer must emit as a kernel-CDF discrete distribution. *)
let rec texpr_contains_sample ((_, _, TAExprNode ae) : texpr) : bool =
  match ae with
  | Sample _ -> true
  | Const _ | Var _ | BoolConst _ | Nil | Unit
  | FinConst _ | RuntimeError _ -> false
  | Let (_, e1, e2) -> texpr_contains_sample e1 || texpr_contains_sample e2
  | Add (e1, e2) | Sub (e1, e2) | Mul (e1, e2) | Div (e1, e2)
  | Cmp (_, e1, e2, _) | FinCmp (_, e1, e2, _, _) | FinEq (e1, e2, _)
  | And (e1, e2) | Or (e1, e2) | Pair (e1, e2) | FuncApp (e1, e2)
  | Cons (e1, e2) | Assign (e1, e2) | Seq (e1, e2) ->
      texpr_contains_sample e1 || texpr_contains_sample e2
  | Not e1 | First e1 | Second e1 | Observe e1
  | Ref e1 | Deref e1 -> texpr_contains_sample e1
  | If (e1, e2, e3) ->
      texpr_contains_sample e1 || texpr_contains_sample e2 || texpr_contains_sample e3
  | Fun (_, e1) | Fix (_, _, e1) -> texpr_contains_sample e1
  | MatchList (e1, e2, _, _, e3) ->
      texpr_contains_sample e1 || texpr_contains_sample e2 || texpr_contains_sample e3
  | DiscreteCase cases ->
      List.exists (fun (b, p) -> texpr_contains_sample b || texpr_contains_sample p) cases
  | Cdf (_, e1) -> texpr_contains_sample e1
  | CdfExpr (k, e1) -> texpr_contains_sample k || texpr_contains_sample e1
  | SpecialFunc (_, args) -> List.exists texpr_contains_sample args

(* If a float-typed expression has a single symbolic identity and a
   finite cut bag of symbolic cuts, return [Some (FinConst idx n)]
   where [idx, n] place the symbolic identity within the cut list.
   Otherwise return [None]. *)
let try_finconst_from_sym ?cut_order_at (ty : ty) : expr option =
  match Ast.force ty with
  | TFloat (cut_bag, _, sym_bag) ->
    (match CutLat.get cut_bag, SymLat.get sym_bag with
     | Finite cut_set, Finite sym_set
       when not (CutSet.is_empty cut_set)
         && SymSet.cardinal sym_set = 1 ->
         let (s, sym_expr) = SymSet.choose sym_set in
         (* Sample-flavored symbolic identities are not fixed
            values -- they're random variables -- so we never
            resolve them to a FinConst here. *)
         if Normalize.contains_sample sym_expr then None
         else
           let all_sym_cuts =
             CutSet.for_all
               (function Less (CVSym _) | LessEq (CVSym _) -> true | _ -> false)
               cut_set
           in
           if all_sym_cuts then
             let idx, modulus =
               Cut_order.idx_and_modulus
                 ?at:cut_order_at
                 (Cut_order.IVSym (s, sym_expr))
                 cut_set
             in
             Some (ExprNode (FinConst (idx, modulus)))
           else None
     | _ -> None)
  | _ -> None

let try_finconst_or_default ?cut_order_at (ty : ty) (default : unit -> expr) : expr =
  match try_finconst_from_sym ?cut_order_at ty with
  | Some e -> e
  | None -> default ()

(* If the given [texpr] is sample-flavored (contains a [Sample]
   somewhere inside it) and its TFloat cut bag is finite and
   non-empty, emit a [DiscreteCase] whose probabilities are kernel-
   CDF expressions of the form [CDF(kernel, cut_i) - CDF(kernel,
   cut_{i-1})].  Returns [None] when no such emission is needed
   (e.g. [te] is a plain constant, or the cut bag is [Top] / empty,
   or [te] does not contain a [Sample]). *)
let try_emit_kernel_discrete ?cut_order_at (ty : ty) (kernel_expr : expr) (te : texpr) : expr option =
  if not (texpr_contains_sample te) then None
  else
    match Ast.force ty with
    | TFloat (cut_bag, _, _) ->
      (match CutLat.get cut_bag with
       | Top -> None
       | Finite cut_set when CutSet.is_empty cut_set -> None
       | Finite cut_set ->
         let cuts = Cut_order.ordered_cuts ?at:cut_order_at cut_set in
         let n = 1 + List.length cuts in
         let cut_point_expr (cv : cut_val) : expr =
           match cv with
           | CVConst f -> ExprNode (Const f)
           | CVSym (_, e) -> e
         in
         let cut_to_expr (c : cut) : expr =
           match c with
           | Less cv | LessEq cv -> cut_point_expr cv
         in
         let cut_exprs = List.map cut_to_expr cuts in
         let mk_cdf p = ExprNode (CdfExpr (kernel_expr, p)) in
         let cases =
           List.init n (fun k ->
             let left_cdf =
               if k = 0 then ExprNode (Const 0.0)
               else mk_cdf (List.nth cut_exprs (k - 1))
             in
             let right_cdf =
               if k = n - 1 then ExprNode (Const 1.0)
               else mk_cdf (List.nth cut_exprs k)
             in
             let prob = ExprNode (Sub (right_cdf, left_cdf)) in
             (ExprNode (FinConst (k, n)), prob)
           )
         in
         Some (ExprNode (DiscreteCase cases)))
    | _ -> None

let discretize ?cut_order_at (e : texpr) : expr =
  (* Helper function for comparison operations *)
  let handle_comparison aux op_name te1 te2 cmp_op flipped =
    let t1, _, _ = te1 in
    let t2, _, _ = te2 in
    let b1 = cut_bag_of t1 (op_name ^ " expects float on left operand") in
    let b2 = cut_bag_of t2 (op_name ^ " expects float on right operand") in
    let val1 = CutLat.get b1 in
    let val2 = CutLat.get b2 in
    if not (CutSetContents.equal val1 val2) then
      failwith ("Internal error: " ^ op_name ^ " operands have different bound bag values despite elaboration");

    (match val1 with
      | Top ->
          ExprNode (Cmp (cmp_op, aux te1, aux te2, flipped))
      | Finite bound_set ->
          let n = 1 + List.length (Cut_order.ordered_cuts ?at:cut_order_at bound_set) in
          let d1 = aux te1 in
          let d2 = aux te2 in
          ExprNode (FinCmp (cmp_op, d1, d2, n, flipped))
    )
  in

  let rec aux ((ty, eff, TAExprNode ae_node) : texpr) : expr =
    match ae_node with
    | Const f ->
        let cuts_bag_ref = cut_bag_of ty "Const expects float" in
        (match CutLat.get cuts_bag_ref with
         | Top -> ExprNode (Const f)
         | Finite cut_set ->
            if CutSet.is_empty cut_set then ExprNode (Const f)
            else
              let all_const_cuts =
                CutSet.for_all
                  (function Less (CVConst _) | LessEq (CVConst _) -> true | _ -> false)
                  cut_set
              in
              if all_const_cuts then
                let idx, modulus =
                  Cut_order.idx_and_modulus
                    ?at:cut_order_at
                    (Cut_order.IVConst f)
                    cut_set
                in
                ExprNode (FinConst (idx, modulus))
              else
                (* Mixed/symbolic cut set with a constant value:
                   per design, programs are assumed uniform, so
                   this should not happen -- pass through. *)
                ExprNode (Const f)
        )

    | BoolConst b -> ExprNode (BoolConst b)

    | Var x ->
        try_finconst_or_default ?cut_order_at ty (fun () -> ExprNode (Var x))

    | Let (x, te1, te2) ->
        ExprNode (Let (x, aux te1, aux te2))

    | Add _ | Sub _ | Mul _ | Div _ ->
        (* Three cases, in priority order:
           1. The arithmetic expression carries a unique symbolic
              identity matching a cut -> emit the corresponding
              [FinConst] (e.g. RHS of a comparison like
              [u() < theta + 1]).
           2. The expression contains a [Sample] and has a finite
              cut bag -> emit a [DiscreteCase] of kernel-CDF
              probabilities (the "non-affine LHS" case).
           3. Otherwise -> keep the arithmetic form. *)
        (match try_finconst_from_sym ?cut_order_at ty with
         | Some e -> e
         | None ->
            let kernel_expr =
              match ae_node with
              | Add (te1, te2) -> ExprNode (Add (texpr_to_expr te1, texpr_to_expr te2))
              | Sub (te1, te2) -> ExprNode (Sub (texpr_to_expr te1, texpr_to_expr te2))
              | Mul (te1, te2) -> ExprNode (Mul (texpr_to_expr te1, texpr_to_expr te2))
              | Div (te1, te2) -> ExprNode (Div (texpr_to_expr te1, texpr_to_expr te2))
              | _ -> failwith "unreachable"
            in
            match try_emit_kernel_discrete ?cut_order_at ty kernel_expr (ty, eff, TAExprNode ae_node) with
            | Some e -> e
            | None -> kernel_expr)
    | SpecialFunc (name, args) ->
        (match try_finconst_from_sym ?cut_order_at ty with
         | Some e -> e
         | None -> ExprNode (SpecialFunc (name, List.map texpr_to_expr args)))
    | Cdf (d, te1) -> ExprNode (Cdf (sample_to_expr d, texpr_to_expr te1))
    | CdfExpr (k, te1) -> ExprNode (CdfExpr (texpr_to_expr k, texpr_to_expr te1))

    | Sample dist_exp ->
        let outer_sample_ty = ty in
        let cuts_bag_of_outer_sample = cut_bag_of outer_sample_ty
          "Internal error: Sample expression's type is not TFloat during discretize"
        in
        let set_or_top_val = CutLat.get cuts_bag_of_outer_sample in

        (match set_or_top_val with
        | Top ->
          (match dist_exp with
          | Distr1 (kind, texpr_arg) ->
            let texpr_arg_discretized = aux texpr_arg in
            ExprNode (Sample (Distr1 (kind, texpr_arg_discretized)))
          | Distr2 (kind, texpr_arg1, texpr_arg2) ->
            let texpr_arg1_discretized = aux texpr_arg1 in
            let texpr_arg2_discretized = aux texpr_arg2 in
            ExprNode (Sample (Distr2 (kind, texpr_arg1_discretized, texpr_arg2_discretized)))
          )
        | Finite outer_cut_set ->
          let outer_cuts = Cut_order.ordered_cuts ?at:cut_order_at outer_cut_set in
          let overall_modulus = 1 + List.length outer_cuts in
          if overall_modulus <= 0 then failwith "Internal error: Sample modulus must be positive";

          let any_sym_cut =
            CutSet.exists
              (function Less (CVSym _) | LessEq (CVSym _) -> true | _ -> false)
              outer_cut_set
          in
          (* Also fall back to symbolic emission when any of the
             distribution's parameters carries a non-empty
             symbolic bag (e.g. [gaussian(mu, 1) < 0.5]). *)
          let param_has_sym (param_texpr : texpr) : bool =
            let param_ty, _, _ = param_texpr in
            match Ast.force param_ty with
            | TFloat (_, _, sym_bag) ->
              (match SymLat.get sym_bag with
               | Top -> false
               | Finite s -> not (SymSet.is_empty s))
            | _ -> false
          in
          let any_sym_param =
            match dist_exp with
            | Distr1 (_, ta) -> param_has_sym ta
            | Distr2 (_, ta1, ta2) -> param_has_sym ta1 || param_has_sym ta2
          in
          let any_sym_cut = any_sym_cut || any_sym_param in

          let default_branch_expr =
            match dist_exp with
            | Distr1 (kind, texpr_arg) -> ExprNode (Sample (Distr1 (kind, aux texpr_arg)))
            | Distr2 (kind, texpr_arg1, texpr_arg2) -> ExprNode (Sample (Distr2 (kind, aux texpr_arg1, aux texpr_arg2)))
          in

          if any_sym_cut then
            (* ============ Symbolic-cut branch ============ *)
            (* Build the symbolic distribution AST.  The CDF's
               distribution argument keeps its ORIGINAL params
               (not the discretized form) so that the CDF formula
               sees the actual symbolic / concrete values. *)
            let distr_ast : expr sample = sample_to_expr dist_exp in
            let cut_point_expr (cv : cut_val) : expr =
              match cv with
              | CVConst f -> mk_const f
              | CVSym (_, e) -> e
            in
            let cut_to_expr (c : cut) : expr =
              match c with
              | Less cv | LessEq cv -> cut_point_expr cv
            in
            let cut_exprs = List.map cut_to_expr outer_cuts in
            let cases =
              List.init overall_modulus (fun k ->
                let left_cdf_expr =
                  if k = 0 then mk_const 0.0
                  else mk_cdf distr_ast (List.nth cut_exprs (k - 1))
                in
                let right_cdf_expr =
                  if k = overall_modulus - 1 then mk_const 1.0
                  else mk_cdf distr_ast (List.nth cut_exprs k)
                in
                let prob_expr = mk_sub right_cdf_expr left_cdf_expr in
                (ExprNode (FinConst (k, overall_modulus)), prob_expr)
              )
            in
            ExprNode (DiscreteCase cases)
          else
            (* ============ Concrete-cut branch (original logic) ============ *)
            let final_expr_producer (concrete_distr : Distributions.cdistr) : expr =
              let get_float_val_from_cut (b: cut) : float =
                match b with
                | Less (CVConst f) | LessEq (CVConst f) -> f
                | _ -> failwith "Internal error: expected constant cut in concrete branch"
              in
              let intervals_for_probs = List.init overall_modulus (fun k_idx ->
                let left_for_cdf =
                  if k_idx = 0 then neg_infinity
                  else get_float_val_from_cut (List.nth outer_cuts (k_idx - 1))
                in
                let right_for_cdf =
                  if k_idx = overall_modulus - 1 then infinity
                  else get_float_val_from_cut (List.nth outer_cuts k_idx)
                in
                (min left_for_cdf right_for_cdf, max left_for_cdf right_for_cdf)
              ) in
              let probs = List.map (fun (l,r) -> prob_cdistr_interval l r concrete_distr) intervals_for_probs in
              if List.exists (fun p -> p < -0.0001 || p > 1.0001) probs then
                  failwith ("Internal error: generated probabilities are invalid: " ^ Pretty.string_of_float_list probs ^ " for distribution " ^ Pretty.string_of_cdistr concrete_distr ^ Printf.sprintf " with %d outer bounds." (List.length outer_cuts));
              let sum_probs = List.fold_left (+.) 0.0 probs in
              if abs_float (sum_probs -. 1.0) > 0.001 then
                 ();
              let distr_cases =
                List.mapi (fun i prob ->
                  (ExprNode (FinConst (i, overall_modulus)),
                   ExprNode (Const (max 0.0 (min 1.0 prob)))))
                  probs
              in
              ExprNode (DiscreteCase distr_cases)
            in

            let get_possible_floats_from_param (param_texpr : texpr) : float list option =
              let param_ty, _, _ = param_texpr in
              match Ast.force param_ty with
              | TFloat (_, consts_bag_ref, _) ->
                (match FloatLat.get consts_bag_ref with
                 | Finite float_set ->
                   if FloatSet.is_empty float_set then None
                   else Some (FloatSet.elements float_set |> List.sort_uniq compare)
                 | Top -> None)
              | _ -> None
            in

            let get_param_modulus_and_cuts (param_texpr : texpr) : (int * cut list) option =
              let param_ty, _, _ = param_texpr in
              match Ast.force param_ty with
              | TFloat (b_bag, _, _) ->
                (match CutLat.get b_bag with
                 | Top -> None
                 | Finite bs_set ->
                   let cuts = Cut_order.ordered_cuts ?at:cut_order_at bs_set in
                   let modulus = 1 + List.length cuts in
                   if modulus <= 0 then None else Some (modulus, cuts))
              | _ -> None
            in

            let rec build_nested_ifs (val_var_name: string) (param_modulus: int) (arms: (expr * expr) list) (default_expr_if_all_fail: expr) : expr =
              match arms with
              | [] ->
                  failwith "build_nested_ifs: Reached empty arms list, this should be handled by caller or indicates an issue."
              | [(_target_finconst_expr, body_expr)] ->
                  body_expr
              | (target_finconst_expr, body_expr) :: rest_arms ->
                let current_val_expr = ExprNode (Var val_var_name) in
                let condition =
                  ExprNode(FinEq(current_val_expr, target_finconst_expr, param_modulus))
                in
                ExprNode (If (condition, body_expr, build_nested_ifs val_var_name param_modulus rest_arms default_expr_if_all_fail))
            in

            let generate_runtime_match_for_param
                ?(already_discretized_expr : expr option = None)
                (param_texpr : texpr)
                (param_name_str : string)
                (build_body_fn : float -> expr)
                (default_expr_for_this_param : expr) : expr =

              match get_possible_floats_from_param param_texpr, get_param_modulus_and_cuts param_texpr with
              | Some possible_floats, Some (param_modulus, param_actual_cuts) ->
                  if List.length possible_floats = 0 then default_expr_for_this_param
                  else if List.length possible_floats = 1 then
                    build_body_fn (List.hd possible_floats)
                  else
                    let actual_discretized_param_expr =
                      match already_discretized_expr with
                      | Some ade -> ade
                      | None -> aux param_texpr
                    in
                    let match_arms = List.map (fun f_val ->
                      let param_consts_bag_for_f = FloatLat.create (Finite (FloatSet.singleton f_val)) in
                      let param_cuts_bag_for_f = CutLat.create (Finite (CutSet.of_list param_actual_cuts)) in
                      let param_sym_bag_for_f = SymLat.create (Finite SymSet.empty) in
                      let texpr_const_f_val = (TFloat(param_cuts_bag_for_f, param_consts_bag_for_f, param_sym_bag_for_f), Pure, TAExprNode (Const f_val)) in
                      let target_finconst_expr = aux texpr_const_f_val in
                      let body = build_body_fn f_val in
                      (target_finconst_expr, body)
                    ) possible_floats in

                    Util.gen_let ("_disc_" ^ param_name_str) actual_discretized_param_expr
                      (fun let_var_name -> build_nested_ifs let_var_name param_modulus match_arms default_expr_for_this_param)
              | _ -> default_expr_for_this_param
            in

            let handle_single_param_distribution kind param_texpr param_name =
              generate_runtime_match_for_param param_texpr param_name
                (fun param_value ->
                  match Distributions.get_cdistr_from_single_arg_kind kind param_value with
                  | Ok dist -> final_expr_producer dist
                  | Error msg -> ExprNode (RuntimeError msg)
                )
                default_branch_expr
            in

            let handle_two_param_distribution kind param1_texpr param1_name param2_texpr param2_name hoist_suffix =
              let discretized_param2_expr = aux param2_texpr in
              let core_logic (eff_discretized_param2_expr : expr) =
                generate_runtime_match_for_param param1_texpr param1_name
                  (fun param1_value ->
                    generate_runtime_match_for_param ~already_discretized_expr:(Some eff_discretized_param2_expr) param2_texpr param2_name
                      (fun param2_value ->
                        match Distributions.get_cdistr_from_two_arg_kind kind param1_value param2_value with
                        | Ok dist -> final_expr_producer dist
                        | Error msg -> ExprNode (RuntimeError msg)
                      )
                      default_branch_expr
                  )
                  default_branch_expr
              in
              (match discretized_param2_expr with
               | ExprNode (Var _) | ExprNode (Const _) | ExprNode (BoolConst _) | ExprNode (FinConst _) ->
                   core_logic discretized_param2_expr
               | _ ->
                   Util.gen_let ("_h_" ^ hoist_suffix) discretized_param2_expr (fun hoisted_var ->
                     core_logic (ExprNode (Var hoisted_var))
                   ))
            in

            match dist_exp with
            | Distr1 (DExponential, texpr_lambda) ->
                handle_single_param_distribution DExponential texpr_lambda "lambda"
            | Distr1 (DLaplace, texpr_scale) ->
                handle_single_param_distribution DLaplace texpr_scale "scale"
            | Distr1 (DCauchy, texpr_scale) ->
                handle_single_param_distribution DCauchy texpr_scale "scale"
            | Distr1 (DTDist, texpr_nu) ->
                handle_single_param_distribution DTDist texpr_nu "nu"
            | Distr1 (DChi2, texpr_nu) ->
                handle_single_param_distribution DChi2 texpr_nu "nu"
            | Distr1 (DLogistic, texpr_scale) ->
                handle_single_param_distribution DLogistic texpr_scale "scale"
            | Distr1 (DRayleigh, texpr_sigma) ->
                handle_single_param_distribution DRayleigh texpr_sigma "sigma"
            | Distr1 (DPoisson, texpr_mu) ->
                handle_single_param_distribution DPoisson texpr_mu "mu"

            | Distr2 (DUniform, texpr_a, texpr_b) ->
                handle_two_param_distribution DUniform texpr_a "a" texpr_b "b" "b"
            | Distr2 (DGaussian, texpr_mu, texpr_sigma) ->
                handle_two_param_distribution DGaussian texpr_mu "mu" texpr_sigma "sigma" "sigma"
            | Distr2 (DBeta, texpr_alpha, texpr_beta_param) ->
                handle_two_param_distribution DBeta texpr_alpha "alpha" texpr_beta_param "beta_param" "beta_p"
            | Distr2 (DLogNormal, texpr_mu, texpr_sigma) ->
                handle_two_param_distribution DLogNormal texpr_mu "mu" texpr_sigma "sigma" "sigma_ln"
            | Distr2 (DGamma, texpr_shape, texpr_scale) ->
                handle_two_param_distribution DGamma texpr_shape "shape" texpr_scale "scale" "scale_g"
            | Distr2 (DPareto, texpr_a, texpr_b) ->
                handle_two_param_distribution DPareto texpr_a "a" texpr_b "b" "b_p"
            | Distr2 (DWeibull, texpr_a, texpr_b) ->
                handle_two_param_distribution DWeibull texpr_a "a" texpr_b "b" "b_w"
            | Distr2 (DGumbel1, texpr_a, texpr_b) ->
                handle_two_param_distribution DGumbel1 texpr_a "a" texpr_b "b" "b_g1"
            | Distr2 (DGumbel2, texpr_a, texpr_b) ->
                handle_two_param_distribution DGumbel2 texpr_a "a" texpr_b "b" "b_g2"
            | Distr2 (DExppow, texpr_a, texpr_b) ->
                handle_two_param_distribution DExppow texpr_a "a" texpr_b "b" "b_ep"
            | Distr2 (DBinomial, texpr_p, texpr_n) ->
                handle_two_param_distribution DBinomial texpr_p "p" texpr_n "n" "b_bi"

        )

    | DiscreteCase cases ->
      let discretized_cases =
        List.map (fun (te_branch, te_prob) -> (aux te_branch, aux te_prob)) cases
      in
      ExprNode (DiscreteCase discretized_cases)

    | Cmp (cmp_op, te1, te2, flipped) ->
        let op_name = match cmp_op with
          | Ast.Lt -> "Less"
          | Ast.Le -> "LessEq"
        in
        handle_comparison aux op_name te1 te2 cmp_op flipped

    | FinCmp (cmp_op, te1, te2, n, flipped) ->
        ExprNode (FinCmp (cmp_op, aux te1, aux te2, n, flipped))

    | FinEq (te1, te2, n) ->
        ExprNode (FinEq (aux te1, aux te2, n))

    | If (te1, te2, te3) ->
        ExprNode (If (aux te1, aux te2, aux te3))

    | Pair (te1, te2) ->
        ExprNode (Pair (aux te1, aux te2))

    | First te ->
        ExprNode (First (aux te))

    | Second te ->
        ExprNode (Second (aux te))

    | Fun (x, te) ->
        ExprNode (Fun (x, aux te))

    | FuncApp (te1, te2) ->
        ExprNode (FuncApp (aux te1, aux te2))

    | FinConst (k, n) ->
        ExprNode (FinConst (k, n))

    | And (te1, te2) ->
        ExprNode (And (aux te1, aux te2))

    | Or (te1, te2) ->
        ExprNode (Or (aux te1, aux te2))

    | Not te1 ->
        ExprNode (Not (aux te1))

    | Observe te1 ->
        ExprNode (Observe (aux te1))

    | Fix (f, x, te_body) ->
        ExprNode (Fix (f, x, aux te_body))

    | Nil -> ExprNode Nil

    | Cons (te_hd, te_tl) ->
        ExprNode (Cons (aux te_hd, aux te_tl))

    | MatchList (te_match, te_nil, y, ys, te_cons) ->
        ExprNode (MatchList (aux te_match, aux te_nil, y, ys, aux te_cons))

    | Ref te1 ->
        ExprNode (Ref (aux te1))
    | Deref te1 ->
        ExprNode (Deref (aux te1))
    | Assign (te1, te2) ->
        ExprNode (Assign (aux te1, aux te2))

    | Seq (te1, te2) ->
        ExprNode (Seq (aux te1, aux te2))

    | Unit -> ExprNode Unit

    | RuntimeError s -> ExprNode (RuntimeError s)

  in
  aux e

let discretize_top ?cut_order_at (e : texpr) : expr =
  let (return_type, _, _) = e in
  Ast.set_cut_bags_to_top return_type;
  discretize ?cut_order_at e
